//===== rAthena Script ======================================= 
//= Renewal Mage Job Quest
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Job Change to Mage Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added support for Baby classes. [Euphy]
//============================================================ 
geffen_in,164,124,4	script	Mage#mage	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				mes "[Mage]";
				mes "Whoa, ça fait longtemps que je ne vous ai pas vu! Vous n'étiez pas sensé être mort?";
				next;
				mes "[Mage]";
				mes "Ah, vous avez du être ressucité. Eh bien, je suis contente de vous avoir de retour.";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[Mage]";
					mes "Je suis désolée, mais je ne pense pas que vous soyez prêt à être initié à la magie. Pourquoi ne finissez-vous pas d'apprendre tous vos Basic Skills?";
					next;
					mes "[Mage]";
					mes "Prenez votre temps. Plus vous apprenez, plus vous serez prêt à être initié à la magie.";
					close;
				}
				mes "[Mage]";
				mes "Bien, comme vous avez déjà passé le test une fois, je ne vous poserai pas de questions sur vos qualifications. Vous voulez récupérer vos sorts tout de suite, n'est-ce pas?";
				next;
				skill 143,0,0;
				jobchange Job_Mage_High;
				skill 157,1,0;
				mes "[Mage]";
				mes "Wow, pour une raison que j'ignore, vous avez bien meilleur allure qu'avant. Bref, je suis sûre que vous ferez un meilleur job en tant que Mage.";
				close;
			}else{
				mes "[Mage]";
				mes "Je peux vous aider pour autre chose? Sinon, pourquoi n'allez-vous pas tester vos compétences? Le monde vous attends~!";
				close;
			}
		}else{
			mes "[Mage]";
			mes "Quoi ? Vous vous interessez à la Guilde des Mages ? Je ne voulais pas vous dire cela, mais cet endroit n'est pas fait pour vous.";
			next;
			mes "[Mage]";
			mes "Je ne sais pas vraiment pourquoi vous êtes encore là, mais je peux vous dire que vous n'êtes pas destiné à être Mage.";
			close;
		}
	}
	mes "[Mage]";
	if(BaseJob != Job_Novice) {
		if(BaseJob == Job_Mage) {
			mes "Hey, vous n'avez pas remarqué ? Vous êtes déjà Mage, imbécile!";
			mes "Un de ces jours vous réaliserez le pouvoir que vous avez en vous quand vous pourrez créer du Feu avec votre esprit!";
		} else
			mes "Hey~ Allons. Arrêtez de jouer. Vous ne pouvez pas être Mage, vous avez déjà un autre métier.";
		close;
	}else{
		mes "Hey?";
		next;
		switch(select("Je veux devenir un Mage:Quels sont les pré-requis pour devenir un Mage?:Rien, merci.")) {
		case 1:
			mes "[Mage]";
			mes "Vous voulez devenir un Mage? Hum...";
			next;
			mes "[Mage]";
			if (Sex) {
				mes "Hey, regardez-vous! Vous êtes plutôt mignon~! Pas mon type cependant...";
			}else{
				mes "Oooh, vous êtes tellement sexy~!";
				mes "J'aime les filles comme vous~";
			}
			mes "Bien, vous dites que vous voulez devenir un Mage?";
			next;
			if(select("Je veux devenir un Mage.:Rien, merci.") == 2) {
				mes "[Mage]";
				mes "Quooii~?! Juste après m'avoir dit que vous vouliez être un Mage, vous changez d'avis?! Réfléchissez-y plus sérieusement!";
				close;
			}
			mes "[Mage]";
			if (getskilllv("NV_BASIC") < 9) {
				mes "Oh, votre niveau de base n'est pas assez bon pour devenir un Mage.";
				mes "Revenez quand vous aurez augmenté vos compétences de base.";
				close;
			}
			mes "Ok.. signez le formulaire.... Wow~ vous avez une super écriture! Ahh, okay. Ca suffit.";
			mes "Vous êtes... "+strcharinfo(0)+".";
			next;
			mes "[Mage]";
			mes "Hmm je vois que vous vous êtes acharné. Même si vous semblez un peu maladroit, je pense que ça ira!";
			mes "Bien! S'en tenir aux bases est la meilleur chose! Je vous transforme tout de suite.";
			next;
			mes "[Mage]";
			mes "Hahh..! Vous êtes un Mage à présent, un de nos collègues!";
			mes "Nous vous souhaitons la bienvenue dans la Guilde des Mages, notre nouvel ami!";
			next;
			mes "[Mage]";
			mes "'Bienvenue dans la Guilde des Mages~'";
			mes "Vous êtes devenu membre de la Guilde des Mages, félicitations! Allez-y!";
			callfunc "Job_Change",Job_Mage;
			getitem 1639,1;	// N_Rod
			close;
		case 2:
			mes "[Mage]";
			mes "Vous voulez devenir un Mage, hein?";
			if (Sex) {
				mes "J'adorerais expliquer les pré-requis à un garçon aussi mignon que vous!";
			}else{
				mes "J'adorerais expliquer les pré-requis à une fille aussi mignonne que vous!";
			}
			next;
			mes "[Mage]";
			mes "Tout d'abord, vous devez atteindre, en tant que débutant, le Niveau Job 10 ainsi qu'apprendre touts les Basic Skills.";
			next;
			mes "[Mage]";
			mes "Auparavant, le test de préparation de potions était assez compliqué. A cause de ça, les candidatures se sont faites plus rares.";
			mes "Donc, nous avons décidé d'accepter tous les candidats qui avaient acquis les pré-requis de base.";
			next;
			mes "[Mage]";
			mes "N'hésitez pas. Soyez juste Magicien!";
			close;
		case 3:
			mes "[Mage]";
			mes "Rien...?";
			close;
		}
	}
}
