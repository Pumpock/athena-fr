//===== rAthena Script ======================================= 
//= Renewal Archer Job Quest
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Job Change to Archer Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added support for Baby classes. [Euphy]
//============================================================ 
payon_in02,64,71,4	script	Archer#archer	85,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && (ADVJOB == Job_Sniper || ADVJOB == Job_Clown || ADVJOB == Job_Gypsy)) {
			mes "[Archer]";
			mes "Hey, je vous connais.";
			mes "Vous avez déjà passé ce test";
			mes "auparavant, n'est-ce pas?";
			next;
			mes "[Archer]";
			mes "Ah, vous venez sûrement";
			mes "du Valhalla après avoir été ressuscité.";
			mes "Wow, c'est si impréssionant!";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Archer]";
				mes "Err...";
				mes "Vous devez apprendre toutes les compétences de base avant de pouvoir devenir Archer.";
				next;
				mes "[Archer]";
				mes "Ok, à plus tard.";
				close;
			}
			mes "[Archer]";
			mes "Eh bien. Je n'ai";
			mes "rien d'autre à ajouter.";
			mes "Je sais que vous deviendrez un grand Archer...";
			next;
			skill 143,0,0;
			jobchange Job_Archer_high;
			skill 147,1,0;
			skill 148,1,0;
			mes "[Archer]";
			mes "Cependant, il n'y a pas de";
			mes "récompense spéciale pour vous cette fois-ci, j'espère que vous comprenez. Prenez bien soin de vous.";
			close;
		}else{
			mes "[Archer]";
			mes "Oh...?";
			mes "Hey, qu'est-ce que";
			mes "vous faites ici...?";
			next;
			mes "[Archer]";
			mes "Je peux vous dire que vous n'êtes pas fait pour être Archer. J'ai comme l'impression que vous";
			mes "êtes destiné à autre chose...";
			close;
		}
	}
	mes "[Archer]";
	mes "Ravi de vous rencontrer. En quoi puis-je vous aider?";
	next;
	switch(select("Je veux devenir un Archer.:Quelles sont vos exigences ?:Rien, merci.")) {
	case 1:
		mes "[Archer]";
		if(BaseJob != Job_Novice) {
			if(BaseJob == Job_Archer)
				mes "Haha, vous vous moquez de moi...";
			else {
				mes "Je suis désolé mais seuls les débutants peuvent changer de métier.";
				mes "Vous avez déjà un bon métier, non?";
			}
			close;
		}else{
			mes "Vous êtes... "+strcharinfo(0)+", c'est ça?";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Archer]";
				mes "Eh bien, vous n'avez pas le bon niveau de compétence.";
				mes "Vous devez atteindre au moins le Niveau Job ^4A4AFF10^000000 et le ^4A4AFFniveau 9^000000 pour vos compétences de base.";
				next;
				mes "[Archer]";
				mes "Car un Archer a besoin d'une extrême concentration, donc nous n'acceptons pas ceux qui ont peu de patience.";
				close;
			}
			mes "[Archer]";
			mes "Vous avez le bon niveau désormais..";
			mes "....Hm~~ donc vous êtes prêt à devenir un Archer. Je vais passer à l'étape suivante tout de suite.";
			next;
			mes "[Archer]";
			mes "Félicitations! Vous êtes Archer à présent! Nous espérons que vous participerez activement à nos différents programmes pour la renaissance de la Guilde des Archers.";
			mes "Ah, des objets sont arrivés du Département de Production. Tenez, prenez ceci! C'est tout à vous!";
			callfunc "Job_Change",Job_Archer;
			getitem 1742,1;	// N_Composite_Bow
			getitem 12004,1;	// Arrow_Container
			getitem 12009,1;	// Silver_Arrow_Container
			getitem 12008,1;	// Fire_Arrow_Container
			next;
			mes "[Archer]";
			mes "Avoir un arc et des flèches, vous êtes un véritable Archer maintenant.";
			mes "Si vous ouvrez votre conteneur de flèches, vous pourrez équiper les flèches qu'il y a dedans.";
			mes "Bien, j'espère avoir de bonnes nouvelles de votre part. Il est temps de se quitter.";
			mes "Au revoir.";
			close;
		}
	case 2:
		mes "[Archer]";
		mes "Un Archer sait se servir d'un arc et a de nombreux talents.";
		mes "La meilleure compétence d'un Archer est de pouvoir attaquer ses ennemis sur une longue distance.";
		next;
		mes "[Archer]";
		mes "Malgré qu'un Archer n'ait pas beaucoup de PV, il ou elle peut tirer sur ses ennemis à grande portée,";
		mes "donc un Archer est plus en sécurité dans un vrai combat.";
		next;
		mes "[Archer]";
		mes "Malgré qu'un Archer dans Ragnarok ait moins de PV, il ou elle a une grande précision et une bonne attaque, ils peuvent donc tuer des monstres avant même que ceux-ci n'approchent.";
		next;
		mes "[Archer]";
		mes "^8C2121Un archer peut changer son métier en Chasseur.^000000";
		mes "^8C2121Autre que Chasseur, vous pouvez aussi devenir Barde si vous êtes un homme, danceuse si vous êtes une femme.^000000";
		close;
	case 3:
		mes "[Archer]";
		mes "Si vous avez des questions, sentez vous libre de me les poser.";
		close;
	}
}
